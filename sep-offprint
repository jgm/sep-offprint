#!/usr/bin/perl -w
#
# sep-offprint 0.4 - John MacFarlane - August 22, 2006
# sep-offprint 0.3 - John MacFarlane - May 25, 2005
#
# produces a PDF or postscript "offprint" of a Stanford 
# Encyclopedia of Philosophy (SEP) article
#
# Argument is an entry name from SEP, as it appears in the URL.
# For example, to get the article on classical logic, which is at
# http://plato.stanford.edu/entries/logic-classical/, just type
#
#   sep-offprint logic-classical
#
# if this script is executable and in your path, otherwise
#
#   perl sep-offprint logic-classical
#
# and it will create logic-classical.pdf. 
#
# There are many command-line options.  For a list, type
#
#   sep-offprint --help
#
# The programs wget, html2ps, and ps2pdf must be in the user's path:
#
# wget can be found at http://www.gnu.org/software/wget/.
#
# html2ps can be found at http://user.it.uu.se/~jan/html2ps.html.
# Download the tarball or zip file and run the "install" script.
#
# ps2pdf is part of Ghostscript -- many users will have it
# already:  http://www.cs.wisc.edu/~ghost/doc/AFPL/get851.htm
#
# For more information and updates, see 
# http://philosophy.berkeley.edu/macfarlane/sep-offprint.html
#

use Getopt::Long;
use File::Temp qw/ tempdir /;
use Cwd;

sub printhelp {
    die 
"Produces a PDF offprint from a Stanford Encyclopedia of Philosophy article.
(http://plato.stanford.edu/)

Usage:                 sep-offprint [options] <entry name>

Examples:              sep-offprint russell
                       sep-offprint frege russell wittgenstein
                       sep-offprint --1up --ps --paper a4 frege
                       sep-offprint --1up --fontfamily Helvetica frege

Options:

--1up                  print one page per sheet, portrait orientation
--2up                  print two pages per sheet, landscape orientation (default) 
--ps                   produce postscript (PS) output
--pdf                  produce PDF output (default)
--font <font>          use <font> (Times, Helvetica, Palatino, Courier) (default Times)
--size <size>          use <size> (10pt, 12pt, 14pt, 16pt) (default 14pt)
--align <align>        use <align> (left, justified) (default justified)
--paper <papersize>    specify <papersize> (letter, legal, a4) (default letter)
--localpath <path>     look for entry in a subdirectory of <path> on local filesystem
--help                 this message
--version              prints version number\n";
}

GetOptions( '1up|1' => \$oneup,
            '2up|2' => \$twoup,
            'ps' => \$ps,
            'pdf' => \$pdf,
            'font=s' => \$fontfamily,
            'size=s' => \$fontsize,
            'align=s' => \$textalign,
            'paper=s' => \$papersize,
            'localpath=s' => \$localpath,
            'help|h' => \$help,
            'version|v' => \$version);

if ($#ARGV < 0) {&printhelp;};
$entryname = $ARGV[0];

# remove uppercase and spaces:
$entryname =~ tr/A-Z/a-z/;
$entryname =~ tr/ /-/;

# remove SEP temp if specified:
$entryname =~ s{http://plato.stanford.edu/entries/}{};
# remove /index.html if specified:
$entryname =~ s{/.*}{};


if ($help) {&printhelp;};
if ($version) {die "sep-offprint ver. 0.3\n";}; 
if (not ($pdf or $ps)) {$pdf=1};
if ($oneup) {$twoup = 0} else {$twoup = 1};
if (not $fontsize) {$fontsize = "14pt"};
if (not $fontfamily) {$fontfamily = "Times"};
if (not $textalign) {$textalign = "justify"};
if (not $papersize) {$papersize = "letter"};

$temp = tempdir ( CLEANUP => 1 );

$dir = getcwd;

# Get all the source files and put them in temp directory $temp

if ($localpath) {
    system("cp $localpath/$entryname/*.* $temp/");
    chdir $temp;
} 
else {
    chdir $temp;
    system("wget --quiet -nd -p http://plato.stanford.edu/entries/$entryname/index.html");
    system("wget --quiet -nd -p http://plato.stanford.edu/entries/$entryname/notes.html");
};



#
# Create blank html file to work around html2ps bug.
#
# Without this blank file after notes.html, html2ps will cut off 
# the last page of an entry if it occurs in the left column in 2up mode.
# 

$blank = "blankhtml" . time;

open FILE, ">$blank" or die "unable to open $blank: $!";

print FILE <<EOF; 
<html>
<head>
<title>&nbsp;</title>
</head>
<body>
<p>&nbsp;</p>
</body>
</html>
EOF

close FILE;

#
# For each argument, create a configuration file with appropriate footers
# and run html2ps and html2pdf on that argument.
#

#
# Create temporary configuration file
#

$html2psrc = "html2psrc" . time;

open FILE, ">$html2psrc" or die "unable to open $html2psrc: $!";

print FILE <<EOF; 
BODY {
    font-size: $fontsize; 
    font-family: $fontfamily; 
    text-align: $textalign; 
}
\@page { 
    margin-left: 2.5cm; 
    margin-right: 2.5cm; 
    margin-top: 2.5cm; 
    margin-bottom: 2.5cm; 
}
\@html2ps { 
    option { 
        twoup: $twoup; 
        landscape: $twoup; 
        number: 0; 
    } 
    paper { type: $papersize } 
	header {
	    right: "STANFORD ENCYCLOPEDIA OF PHILOSOPHY";
		left: \$T;
    }
    footer {
        left: \$N;
        right: "http://plato.stanford.edu/entries/$entryname";
    }
}
EOF

close FILE;

# name of temporary file to hold postscript output of html2ps
$pstemp = $entryname . time;

# preprocess html:
# takes filename as parameter
# remove navigation bars, etc.
# replace &#9633; entity reference with appropriate image 

sub preprocess_html {
	my $file = $_[0];
	{
		local( $/, *FILE ); 
		open(FILE, "< $file") or die "Couldn't open $file to read";
		$contents = <FILE>;
		close(FILE);
	}
	$contents =~ s/<body>.*?<h1>/<body><div id="content"><h1>/s;
	$contents =~ s/&\#9633;/<img alt="Box" src="http:\/\/plato.stanford.edu\/symbols\/Box.gif">/g;
	open(FILE, "> $file") or die "Couldn't open $file to write"; 
	print FILE $contents;
	close(FILE);
}

if (-e "notes.html") { $notes = "notes.html"; preprocess_html("notes.html"); } else { $notes = ""; };

preprocess_html("index.html");

system("html2ps -D -f $html2psrc -o $pstemp index.html $notes $blank");

if ($pdf) {system("ps2pdf -sPAPERSIZE=$papersize $pstemp $dir/$entryname.pdf") || print "Created $entryname.pdf.\n";};

if ($ps) {rename($pstemp, "$dir/$entryname.ps"); print "Created $entryname.ps\n";};

# note: temporary directory will be deleted on exit

chdir $dir;

